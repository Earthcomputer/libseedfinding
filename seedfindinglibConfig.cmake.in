# Generates a macro to auto-configure everything
@PACKAGE_INIT@

# Setting these here so they're accessible after install.
# Might be useful for some users to check which settings were used.
set(SEEDFINDINGLIB_IS_USING_CUDA @SEEDFINDINGLIB_IS_USING_CUDA@)
set(SEEDFINDINGLIB_IS_COMPILED @SEEDFINDINGLIB_COMPILE@)
set(SEEDFINDINGLIB_VERSION @PROJECT_VERSION@)

include(CMakeFindDependencyMacro)

# We add find_dependency calls here to not make the end-user have to call them.
if(@SEEDFINDINGLIB_IS_USING_CUDA@)
    enable_language(CUDA)
	find_dependency(CUDA REQUIRED)
endif()

# Mildly useful for end-users
# Not really recommended to be used though
set_and_check(SEEDFINDINGLIB_INCLUDE_DIR "@PACKAGE_CMAKE_INSTALL_FULL_INCLUDEDIR@")
# Lets the end-user find the header path with the header appended
# This is helpful if you're using Cmake's pre-compiled header feature
set_and_check(SEEDFINDINGLIB_HEADER_PATH "@PACKAGE_CMAKE_INSTALL_FULL_INCLUDEDIR@/seedfindinglib.h")

# Brings in the target library
include("${CMAKE_CURRENT_LIST_DIR}/seedfindinglibTargets.cmake")

# Ouputs a "found seedfindinglib /usr/include/seedfindinglib.h" message when using find_package(seedfindinglib)
include(FindPackageMessage)
if(TARGET seedfindinglib::seedfindinglib)
	set(SEEDFINDINGLIB_FOUND TRUE)

	# Since the compiled version has a lib, show that in the message
	if(@SEEDFINDINGLIB_COMPILE@)
		# The list of configurations is most likely just 1 unless they installed a debug & release
		get_target_property(_seedfindinglib_configs seedfindinglib::seedfindinglib "IMPORTED_CONFIGURATIONS")
		# Need to loop since the "IMPORTED_LOCATION" property isn't want we want.
		# Instead, we need to find the IMPORTED_LOCATION_RELEASE or IMPORTED_LOCATION_DEBUG which has the lib path.
		foreach(_seedfindinglib_conf "${_seedfindinglib_configs}")
			# Grab the path to the lib and sets it to SEEDFINDINGLIB_LIBRARY
			get_target_property(SEEDFINDINGLIB_LIBRARY seedfindinglib::seedfindinglib "IMPORTED_LOCATION_${_seedfindinglib_conf}")
			# Check if we found it
			if(SEEDFINDINGLIB_LIBRARY)
				break()
			endif()
		endforeach()

		unset(_seedfindinglib_configs)
		unset(_seedfindinglib_conf)

		find_package_message(seedfindinglib "Found seedfindinglib: ${SEEDFINDINGLIB_LIBRARY} (found version \"${SEEDFINDINGLIB_VERSION}\")" "[${SEEDFINDINGLIB_LIBRARY}][${SEEDFINDINGLIB_HEADER_PATH}]")
	else()
		find_package_message(seedfindinglib "Found seedfindinglib: ${SEEDFINDINGLIB_HEADER_PATH} (found version \"${SEEDFINDINGLIB_VERSION}\")" "[${SEEDFINDINGLIB_HEADER_PATH}]")
	endif()
endif()
